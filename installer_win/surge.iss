; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppPublisher "Vember Audio"
#define MyAppURL "http://www.vemberaudio.se"
#define MyAppName "Surge"
#define MyAppVersion GetEnv('SURGE_VERSION')
#define MyID "650E559A-2F44-44FE-861F-4108AE4BC30E"

#if MyAppVersion == ""
#define MyAppVersion "0.0.0"
#endif

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
ArchitecturesInstallIn64BitMode=x64
ArchitecturesAllowed=x64
AppId={#MyID}
AppName="{#MyAppName} {#MyAppVersion}"
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={reg:HKLM\SOFTWARE\VST,VSTPluginsPath|{cf}\VST3}
DefaultGroupName=Surge
DisableProgramGroupPage=yes
LicenseFile=..\LICENSE
OutputBaseFilename="{#MyAppName}-{#MyAppVersion}-Setup"
SetupIconFile=surge.ico
UsePreviousAppDir=no
Compression=lzma
SolidCompression=yes
UninstallFilesDir={{commmonappdata}\Surge}

;; since the fx bank is now a dir not a file we have to force delete the old version in case it was a file
[InstallDelete]
Type: filesandordirs; Name: "{cf}\VST3\SurgeEffectsBank.vst3"

[Components]
Name: Data; Description: Data files; Types: full compact custom; Flags: fixed
Name: VST3; Description: VST3 Plug-in (64 bit); Types: full compact custom; Flags: checkablealone
Name: EffectsVST3; Description: SurgeEffectsBank VST3 Plug-in (64 bit); Types: full compact custom; Flags: checkablealone

[Files]
Source: ..\resources\data\*; DestDir: {commonappdata}\Surge; Components: Data; Flags: recursesubdirs; Excludes: "*.git";
Source: ..\resources\fonts\Lato-Regular.ttf; DestDir: "{fonts}"; Components: Data; FontInstall: "Lato"; Flags: onlyifdoesntexist uninsneveruninstall
Source: ..\build\surge_products\Surge.vst3; DestDir: {cf}\VST3; Components: VST3; Flags: ignoreversion
Source: ..\surge-fx\build\product\SurgeEffectsBank.vst3; DestDir: {cf}\VST3; Components: EffectsVST3; Flags: ignoreversion skipifsourcedoesntexist recursesubdirs

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Icons]
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"

[Run]
Filename: "{cmd}"; \
    WorkingDir: "{cf}\VST3"; \
    Parameters: "/C mklink /D /J  ""{cf}\VST3\SurgeData"" ""{commonappdata}\Surge"""; \
    Description: "Install Surge portable by hard linking resources to VST3 Directory"; \
    Flags: postinstall unchecked runascurrentuser